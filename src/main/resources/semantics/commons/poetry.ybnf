#YBNF 1.0 utf8;
callable @poetry(title), @poetry(sentence), @poetry(author);

$poetry_author{author} = $_call_poetry_author_;
$poetry_title{title} = $_call_poetry_title_;
$poetry_sentence{sentence} = $_call_poetry_sentence_;

$who = 我|给我;
$want1=想|要|想要;
$want2=可以|能不能|可不可以;
$intent=背|念|读|来|听|背诵;
$quantifier=[换](一|一首|一个|个|首);
$poetry=诗|唐诗|诗词|诗句|句子|(其(他|她|它|余)[的]$poetry);
$var1=(是|叫)(谁|哪个|哪位|什么)(写 |创作) [的];
$from=出自|属于|是|来自;

$case1=([$who] [再] [$want1])|([$want2] [$who] [再]);
$case2=$poetry_sentence|$poetry_title|$poetry_author;

$poetry_case1{intent%queryPoetry}=$case1[$intent][$quantifier][$poetry_author][的][$case2][$poetry];
$poetry_case2{intent%prevSentence}=$poetry_sentence [的] (上 | 前) [面] 一句 [是 ([什么]|[哪个])];
$poetry_case3{intent%nextSentence}=$poetry_sentence [的] (下 | 后) [面] 一句 [是 ([什么]|[哪个])];

$poetry_case4_1=$case2 [的作者]$var1;
$poetry_case4_2=$var1 $case2;
$poetry_case4{intent%queryAuthor} = $poetry_case4_2|$poetry_case4_1;

$poetry_case5{intent%queryTitle} = $poetry_sentence $from [哪$quantifier] 诗 [[里 | 中] 的] [$poetry];
$poetry_case6{intent%queryDynasty} = $case2 $from (哪个|什么) 朝代 [的 [$poetry]];

$poetry_common = ($poetry_case6|$poetry_case5|$poetry_case4|$poetry_case3|$poetry_case2|$poetry_case1)[$_yyd_particle_];
