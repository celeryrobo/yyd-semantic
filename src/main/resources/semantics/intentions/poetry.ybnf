#YBNF 1.0 utf8;
#include original.ybnf;
service poetry;
callable @poetry(title), @poetry(sentence), @poetry(author);
root $main;

$poetry_author{author} = $_call_poetry_author_;
$poetry_title{title} = $_call_poetry_title_;
$poetry_sentence{sentence} = $_call_poetry_sentence_;

$poetry_case1_1 = ((我 (要 | 想) 听) | 来 | (背 [诵]) | 念 | 读) [[一] 首] [$poetry_author 的] ($poetry_sentence | $poetry_title | ([唐] 诗));
$poetry_case1_2 = [换 [一] 首] $poetry_author 的 [$poetry_sentence | $poetry_title | ([唐] 诗)];
$poetry_case1_3 = $poetry_title;
$poetry_case1_4 = (换 [一] 首) 其 (它 | 他) [[诗] 人的 [唐]] 诗 [句];
$poetry_case1_5 = [再 | 继续] ((换 [一] 首) | 下一首);
$poetry_case1{intent%queryPoetry} = $poetry_case1_5|$poetry_case1_4|$poetry_case1_3|$poetry_case1_2|$poetry_case1_1;

$poetry_case2{intent%prevSentence} = [$poetry_sentence] [的] (上 | 前) [面] 一句 [是 [什么]];

$poetry_case3_1 = [$poetry_sentence] [的] (下 | 后) [面] 一句 [是 [什么]];
$poetry_case3_2 = $poetry_sentence;
$poetry_case3{intent%nextSentence} = $poetry_case3_2|$poetry_case3_1;

$poetry_case4_1 = [(这 [一] (首|句) 诗) | $poetry_sentence | $poetry_title] [的] 作者  [是 (谁 | (哪 [一] 个) | (哪 [一] 位))];
$poetry_case4_2 = [(这 [一] (首|句) 诗) | $poetry_sentence | $poetry_title] 是 (谁 | (哪 [一] 个) | (哪 [一] 位)) (写 | 创作) 的;
$poetry_case4_3 = [是] (谁 | 哪个 | 哪位) (写 | 创作) 的 ($poetry_sentence | $poetry_title | 这首诗);
$poetry_case4{intent%queryAuthor} = $poetry_case4_3|$poetry_case4_2|$poetry_case4_1;

$poetry_case5_1 = ((这 [一] 句 [诗]) | $poetry_sentence) (出自 | 属于 | 是) ((哪 [一] (个 | 首)) | 什么) 诗 [[里 | 中] 的  [诗句 | 句子]];
$poetry_case5_2 = [这 [首]] 诗 [是] [叫] (什么|的) (名字|标题);
$poetry_case5{intent%queryTitle} = $poetry_case5_2|$poetry_case5_1;

$poetry_case6_1 = ((这 [一] (首|句) [诗]) | $poetry_sentence | $poetry_title | $poetry_author) [是] [属于] ((哪 [一] 个) | 什么) 朝代 [的];
$poetry_case6_2 = ((这 [一] (首|句) [诗]) | $poetry_sentence | $poetry_title | $poetry_author) 的朝代 [是 ((哪 [一] 个) | 什么)];
$poetry_case6{intent%queryDynasty} = $poetry_case6_2|$poetry_case6_1;

$poetry_case7{intent%thisPoetry} = (背|念|读) [诵|([一] 下)] 这 [首] 诗 [[的] 内容];

$main = $poetry_case7|$poetry_case6|$poetry_case5|$poetry_case4|$poetry_case3|$poetry_case2|$poetry_case1;
